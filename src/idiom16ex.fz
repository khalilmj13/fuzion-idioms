# a binTreeNode is a value plus left and right sub-trees
#
binTreeNode(T type : ordered T,
            val T,
            left, right binTree T) ref
is

  # add element to this tree, return resulting tree
  #
  add (v T) binTree T is
    l := if (v < val) (left .add v) else left
    r := if (v > val) (right.add v) else right
    binTreeNode val l r


# a binTree is either a binTreeNode or an empty tree represented by nil
#
binTree(T type : ordered T) : choice (binTreeNode T) nil is

  # add element to this tree, return resulting tree
  #
  add (v T) binTree T is
    match binTree.this
      nil           => binTreeNode v nil nil
      n binTreeNode => n.add v

  # traverse this tree
  #
  traverse (f T -> unit) unit is
    match binTree.this
      nil           =>
      n binTreeNode =>
        n.left.traverse f
        f n.val
        n.right.traverse f

for
  tree binTree String := nil, tree.add x
  x in ["dog", "elephant", "cat", "cat", "cat", "cat", "lion", "ant", "bee", "albatros", "hyena", "horse", "frog"]
else
  tree.traverse (s -> say s)
